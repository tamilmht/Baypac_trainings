<xsl:stylesheet version = "1.0" xmlns:xsl = "http://www.w3.org/1999/XSL/Transform" xmlns:peci="urn:com.workday/peci">
    <xsl:output method = "xml" indent = "yes" encoding="UTF-8"/>

    <xsl:template match = "peci:Workers_Effective_Stack">
        <root>
            <peci_summary>
                <xsl:apply-templates select = "peci:Summary"/>
            </peci_summary>

            <peci_worker_summary>
                <xsl:apply-templates select = "peci:Worker/peci:Worker_Summary"/>
            </peci_worker_summary>

            <peci_effective_change>
                <xsl:apply-templates select = "peci:Worker/peci:Effective_Change"/>
            </peci_effective_change>

            <peci_worker_status>
                <xsl:apply-templates select = "peci:Worker/peci:Effective_Change/peci:Worker_Status"/>
            </peci_worker_status>

            <peci_position>
                <xsl:apply-templates select = "peci:Worker/peci:Effective_Change/peci:Position"/>
            </peci_position>

            <peci_compensation>
                <xsl:apply-templates select = "peci:Worker/peci:Effective_Change/peci:Compensation"/>
            </peci_compensation>

            <peci_compensation_plans>
                <xsl:apply-templates select = "peci:Worker/peci:Effective_Change/peci:Compensation_Plans"/>
            </peci_compensation_plans>

        </root>
    </xsl:template>
    
    
    
    <xsl:template match = "peci:Summary">
        <row>
            <xsl:apply-templates select = "peci:*"/>
        </row>
    </xsl:template>
    <xsl:template match = "peci:Summary/peci:*">
        <xsl:variable name = "elementName" select = "local-name()"/>
        <xsl:element name = "{$elementName}"><xsl:value-of select = "."/></xsl:element>
    </xsl:template>

    <xsl:template match = "peci:Worker_Summary">
        <row>
            <WID><xsl:value-of select = "peci:WID"/></WID>
            <Employee_ID><xsl:value-of select = "peci:Employee_ID"/></Employee_ID>
            <Name><xsl:value-of select = "peci:Name"/></Name>
        </row>
    </xsl:template>

    <xsl:template match = "peci:Effective_Change">
        <row>
            <Employee_ID><xsl:value-of select = "../peci:Worker_Summary/peci:Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "@peci:Sequence"/></Effseq>
            <Derived_Event_Code><xsl:value-of select = "peci:Derived_Event_Code"/></Derived_Event_Code>
            <Effective_Moment><xsl:value-of select = "peci:Effective_Moment"/></Effective_Moment>
            <Entry_Moment><xsl:value-of select = "peci:Entry_Moment"/></Entry_Moment>
        </row>
    </xsl:template>

    <xsl:template match = "peci:Worker_Status">
        <row>
            <Employee_ID><xsl:value-of select = "../../peci:Worker_Summary/peci:Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "../@peci:Sequence"/></Effseq>
            <Action>
                <xsl:choose>
                    <xsl:when test = "@peci:isUpdated='1'">U</xsl:when>
                    <xsl:when test = "@peci:isAdded='1'">A</xsl:when>
                    <xsl:otherwise>N</xsl:otherwise>
                </xsl:choose>
            </Action>
            <xsl:apply-templates select = "peci:*"/>
        </row>
    </xsl:template>

    <xsl:template match = "peci:Position">
        <xsl:variable name = "Pos_Seq" select = "position()"/>
        <xsl:variable name = "Employee_ID" select = "../../peci:Worker_Summary/peci:Employee_ID"/>
        <xsl:variable name = "Effseq" select ="../@peci:Sequence"/>
        <row>
            <Employee_ID><xsl:value-of select = "$Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "$Effseq"/></Effseq>
            <Action>
                <xsl:choose>
                    <xsl:when test = "@peci:isUpdated='1'">U</xsl:when>
                    <xsl:when test = "@peci:isAdded='1'">A</xsl:when>
                    <xsl:otherwise>N</xsl:otherwise>
                </xsl:choose>
            </Action>
            <Pos_Seq><xsl:value-of select = "$Pos_Seq"/></Pos_Seq>
            <xsl:apply-templates select = "peci:*[not(self::peci:Job_Family or self::peci:Business_Site or self::peci:Supervisor or self::peci:Organization)]"/>
        
            <peci_pos_job_family>
                <xsl:apply-templates select ="peci:Job_Family">
                    <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
                    <xsl:with-param name = "Effseq" select = "$Effseq"/>
                    <xsl:with-param name = "elementSeq" select = "$Pos_Seq"/>
                    <xsl:with-param name = "elementSeqName" select = "'Pos_Seq'"/>
                </xsl:apply-templates>
            </peci_pos_job_family>
            <peci_pos_business_site>
                <xsl:apply-templates select ="peci:Business_Site">
                    <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
                    <xsl:with-param name = "Effseq" select = "$Effseq"/>
                    <xsl:with-param name = "elementSeq" select = "$Pos_Seq"/>
                    <xsl:with-param name = "elementSeqName" select = "'Pos_Seq'"/>
                </xsl:apply-templates>
            </peci_pos_business_site>
            <peci_pos_Supervisor>
                <xsl:apply-templates select ="peci:Supervisor">
                    <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
                    <xsl:with-param name = "Effseq" select = "$Effseq"/>
                    <xsl:with-param name = "elementSeq" select = "$Pos_Seq"/>
                    <xsl:with-param name = "elementSeqName" select = "'Pos_Seq'"/>
                </xsl:apply-templates>
            </peci_pos_Supervisor>
            <peci_pos_organization>
                <xsl:apply-templates select ="peci:Organization">
                    <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
                    <xsl:with-param name = "Effseq" select = "$Effseq"/>
                    <xsl:with-param name = "elementSeq" select = "$Pos_Seq"/>
                    <xsl:with-param name = "elementSeqName" select = "'Pos_Seq'"/>
                </xsl:apply-templates>
            </peci_pos_organization>
        </row>
    </xsl:template>

    <xsl:template match = "peci:Compensation">
        <row>
            <Employee_ID><xsl:value-of select = "../../peci:Worker_Summary/peci:Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "../@peci:Sequence"/></Effseq>
            <Action>
                <xsl:choose>
                    <xsl:when test = "@peci:isUpdated='1'">U</xsl:when>
                    <xsl:when test = "@peci:isAdded='1'">A</xsl:when>
                    <xsl:otherwise>N</xsl:otherwise>
                </xsl:choose>
            </Action>
            <xsl:apply-templates select = "peci:*"/>
        </row>
    </xsl:template>

    <xsl:template match = "peci:Compensation_Plans">
        
        <xsl:variable name = "Employee_ID" select = "../../peci:Worker_Summary/peci:Employee_ID"/>
        <xsl:variable name = "Effseq" select = "../@peci:Sequence"/> 
        <xsl:variable name = "Comp_Plans_Seq" select = "position()"/>
        <row>
            <Employee_ID><xsl:value-of select = "$Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "$Effseq"/></Effseq>
            <Action>
                <xsl:choose>
                    <xsl:when test = "@peci:isUpdated='1'">U</xsl:when>
                    <xsl:when test = "@peci:isAdded='1'">A</xsl:when>
                    <xsl:otherwise>N</xsl:otherwise>
                </xsl:choose>
            </Action>
            <Comp_Plans_Seq><xsl:value-of select = "$Comp_Plans_Seq"/></Comp_Plans_Seq>
            
            <xsl:for-each select = "peci:*">
                <xsl:choose>
                    <xsl:when test = "local-name() = 'Salary_and_Hourly_Plan' or local-name() = 'Bonus_Plan' ">
                        <xsl:call-template name = "Nested_Child">
                            <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
                            <xsl:with-param name = "Effseq" select = "$Effseq"/>
                            <xsl:with-param name = "elementSeq" select = "$Comp_Plans_Seq"/>
                            <xsl:with-param name = "elementSeqName" select = "'Comp_Plans_Seq'"/>
                        </xsl:call-template>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:call-template name = "Name_ACTN_PV">
                            <xsl:with-param name = 'element' select = "."/>
                            <xsl:with-param name = 'elementName' select = "local-name()"/>
                        </xsl:call-template>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:for-each>
        </row>
    </xsl:template>

    <!-- Logic Template Caller -->
    <xsl:template match = "peci:Worker_Status/peci:* | peci:Position/peci:*[not(self::peci:Job_Family or self::peci:Business_Site or self::peci:Supervisor or self::peci:Organization)] | peci:Compensation/peci:*[self::peci:Position_ID]">
        <xsl:call-template name = "Name_ACTN_PV">
            <xsl:with-param name = 'element' select = "."/>
            <xsl:with-param name = 'elementName' select = "local-name()"/>
        </xsl:call-template>
    </xsl:template>

    <xsl:template match = "peci:Position/peci:Job_Family | peci:Position/peci:Business_Site | peci:Position/peci:Supervisor | peci:Position/peci:Organization ">
        <xsl:param name = "Employee_ID"/>
        <xsl:param name = "Effseq"/>
        <xsl:param name = "elementSeq"/>
        <xsl:param name = "elementSeqName"/>
        <xsl:call-template name = "Nested_Child">
            <xsl:with-param name = "Employee_ID" select = "$Employee_ID"/>
            <xsl:with-param name = "Effseq" select = "$Effseq"/>
            <xsl:with-param name = "elementSeq" select = "$elementSeq"/>
            <xsl:with-param name = "elementSeqName" select = "$elementSeqName"/>
        </xsl:call-template>
    </xsl:template>

    <!-- Logic Template -->

    <xsl:template name = "Nested_Child">
        <xsl:param name = "Employee_ID"/>
        <xsl:param name = "Effseq"/>
        <xsl:param name = "elementSeq"/>
        <xsl:param name = "elementSeqName"/>
        <xsl:variable name = "Seq" select = "position()"/>
        <row>
            <Employee_ID><xsl:value-of select = "$Employee_ID"/></Employee_ID>
            <Effseq><xsl:value-of select = "$Effseq"/></Effseq>
            <Action>
                <xsl:choose>
                    <xsl:when test = "@peci:isUpdated='1'">U</xsl:when>
                    <xsl:when test = "@peci:isAdded='1'">A</xsl:when>
                    <xsl:otherwise>N</xsl:otherwise>
                </xsl:choose>
            </Action>
            <xsl:element name = "{$elementSeqName}"><xsl:value-of select = "$elementSeq"/></xsl:element>
            <seq><xsl:value-of select = "$Seq"/></seq>
            <xsl:for-each select = "peci:*">
                <xsl:call-template name = "Name_ACTN_PV">
                    <xsl:with-param name = "element" select = "."/>
                    <xsl:with-param name = "elementName" select = "local-name()"/>
                </xsl:call-template>
            </xsl:for-each> 
        </row>
    </xsl:template>


    <xsl:template name = "Name_ACTN_PV">
        <xsl:param name = "element"/>
        <xsl:param name = "elementName"/>

        <xsl:variable name = "elementNameACTN" select = "concat($elementName,'_ACTN')"/>
        <xsl:variable name = "elementNamePV" select = "concat($elementName,'_PV')"/>

        <xsl:element name = "{$elementName}">
            <xsl:value-of select = "$element"/>
        </xsl:element>
        <xsl:element name = "{$elementNameACTN}">
            <xsl:choose>
                <xsl:when test = "@peci:isAdded = '1'">A</xsl:when>
                <xsl:when test = "@peci:isDeleted = '1'">D</xsl:when>
            </xsl:choose>
        </xsl:element>
        <xsl:element name = "{$elementNamePV}">
            <xsl:value-of select = "@peci:priorValue"/>
        </xsl:element>
    </xsl:template>

</xsl:stylesheet>